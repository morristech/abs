# $Id: PKGBUILD 166855 2012-09-19 22:46:12Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=apache
pkgver=2.4.3
pkgrel=1
pkgdesc='A high performance Unix-based HTTP server (tealc edition)'
arch=('i686' 'x86_64')
options=('!libtool')
url='http://www.apache.org/dist/httpd'
license=('APACHE')
backup=(etc/conf.d/apache etc/httpd/conf/httpd.conf
        etc/httpd/conf/extra/httpd-{autoindex,dav,default,info,languages}.conf
        etc/httpd/conf/extra/httpd-{manual,mpm,multilang-errordoc}.conf
        etc/httpd/conf/extra/httpd-{ssl,userdir,vhosts}.conf
        etc/logrotate.d/httpd)
depends=('openssl' 'zlib' 'apr-util' 'pcre')
optdepends=('lynx: apachectl status')
install=apache.install
source=(http://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2
        http://www.apache.org/dist/httpd/httpd-${pkgver}.tar.bz2.asc
        apachectl-confd.patch
        apache.conf.d
        apache.tmpfiles.conf
        httpd.logrotate
        httpd
        httpd.service
        arch.layout)
md5sums=('87aaf7bc7e8715f0455997bb8c6791aa'
         'c65a2229a7d15bc6f0e9730fc929a5c7'
         '4ac64df6e019edbe137017cba1ff2f51'
         '08b3c875f6260644f2f52b4056d656b0'
         '82068753dab92fe86312b1010a2904d7'
         '6382331e9700ed9e8cc78ea51887b537'
         'c7e300a287ef7e2e066ac7639536f87e'
         'ed219c8228d4d5ae9b7a55107cf79706'
         '3d659d41276ba3bfcb20c231eb254e0c')

build() {
	cd "${srcdir}/httpd-${pkgver}"

	patch -Np0 -i "${srcdir}/apachectl-confd.patch"

	# set default user
	sed -e 's#User daemon#User http#' \
	    -e 's#Group daemon#Group http#' \
	    -i docs/conf/httpd.conf.in

	cat "${srcdir}/arch.layout" >> config.layout

	./configure --enable-layout=Arch \
        --enable-mpms-shared='prefork worker event' \
        --enable-so \
        --enable-modules=all \
        --enable-mods-shared=all \
        --enable-ssl --with-ssl \
        --enable-suexec \
        --enable-deflate \
        --with-suexec-caller=http \
        --with-suexec-docroot=/srv/http \
        --with-suexec-logfile=/var/log/httpd/suexec.log \
        --with-suexec-bin=/usr/sbin/suexec \
        --with-suexec-uidmin=99 --with-suexec-gidmin=99 \
        --with-pcre=/usr \
        --with-apr=/usr/bin/apr-1-config \
        --with-apr-util=/usr/bin/apu-1-config

	make DESTDIR="${pkgdir}" install
	install -m755 httpd "${pkgdir}/usr/sbin/httpd"

	install -D -m755 "${srcdir}/httpd" "${pkgdir}/etc/rc.d/httpd"
	install -D -m644 "${srcdir}/httpd.logrotate" "${pkgdir}/etc/logrotate.d/httpd"
	install -D -m644 "${srcdir}/apache.conf.d" "${pkgdir}/etc/conf.d/apache"
	install -D -m644 "${srcdir}/apache.tmpfiles.conf" "${pkgdir}/usr/lib/tmpfiles.d/apache.conf"

	# symlinks for /etc/httpd
	ln -fs /var/log/httpd "${pkgdir}/etc/httpd/logs"
	ln -fs /run/httpd "${pkgdir}/etc/httpd/run"
	ln -fs /usr/lib/httpd/modules "${pkgdir}/etc/httpd/modules"
	ln -fs /usr/lib/httpd/build "${pkgdir}/etc/httpd/build"

	# set sane defaults
	sed -e 's#/usr/lib/httpd/modules/#modules/#' \
	    -e 's|#\(Include conf/extra/httpd-autoindex.conf\)|\1|' \
	    -e 's|#\(Include conf/extra/httpd-default.conf\)|\1|' \
	    -i "${pkgdir}/etc/httpd/conf/httpd.conf"

	# cleanup
	rm -rf "${pkgdir}/usr/share/httpd/manual"
	rm -rf "${pkgdir}/etc/httpd/conf/original"
	rm -rf "${pkgdir}/srv/"
	rm -rf "${pkgdir}/var/run"


    install -m755 -d "${pkgdir}/usr/lib/systemd/system"
    install -m644 "${srcdir}/httpd.service" "${pkgdir}/usr/lib/systemd/system/"
}
